pkgname=binutils
pkgver=2.29.0
pkgrel=1
_commit=dd9a28c0966d13924fbd1096a724ae334954d830
pkgdesc="A set of programs to assemble and manipulate binary and object files"
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('libz1')
makedepends=('git')
options=('staticlibs' '!distcc' '!ccache')
provides=('binutils-symlinks')
conflicts=('binutils-symlinks')
source=(git+https://sourceware.org/git/binutils-gdb.git#commit=${_commit})
md5sums=('SKIP')

prepare() {
  cd binutils-gdb

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir -p ${srcdir}/binutils-build
}

build() {
  cd binutils-build

  ../binutils-gdb/configure \
    --prefix=/usr \
    --enable-threads \
    --enable-shared \
    --enable-ld=default \
    --enable-gold \
    --enable-plugins \
    --enable-relro \
    --with-pic \
    --disable-werror \
    --disable-gdb

  # check the host environment and makes sure all the necessary tools are available
  make configure-host
  make tooldir=/Usr
}

package() {
  cd binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install

  # Remove unwanted files
  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )" > "$pkgdir/usr/lib/libbfd.so"
  echo "INPUT( /usr/lib/libopcodes.a -lbfd )" > "$pkgdir/usr/lib/libopcodes.so"
}
