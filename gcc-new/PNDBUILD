pkgname=gcc-new
pkgver=7.1.1
_pkgver=${pkgver:0:1}
_islver=0.18
pkgrel=4
_commit=d791474f3fc2133fa0c310e566988b0cbdff321e
pkgdesc="The GNU Compiler Collection"
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org"
depends=('libmpfr3' 'libmpc')
makedepends=('binutils' 'git')
options=('!emptydirs')
source=(git+https://gcc.gnu.org/git/gcc.git#commit=${_commit})
md5sums=('SKIP')

prepare() {
  cd ${srcdir}/gcc

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  mkdir -p ${srcdir}/gcc-build
}

build() {
  cd ${srcdir}/gcc-build

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  ${srcdir}/gcc/configure --prefix=/usr/ngcc \
      --enable-languages=c,c++ \
      --enable-shared \
      --enable-threads=posix \
      --with-system-zlib \
      --enable-__cxa_atexit \
      --disable-libunwind-exceptions \
      --enable-clocale=gnu \
      --disable-libstdcxx-pch \
      --disable-libssp \
      --enable-gnu-unique-object \
      --enable-linker-build-id \
      --enable-lto \
      --enable-plugin \
      --enable-install-libiberty \
      --with-linker-hash-style=gnu \
      --enable-gnu-indirect-function \
      --disable-multilib \
      --disable-werror \
      --enable-checking=release \
      --enable-default-pie \
      --enable-default-ssp
  make
}

package()
{
  pkgdesc="Runtime libraries shipped by GCC"
  options=('!emptydirs' '!strip')

  cd ${srcdir}/gcc-build
  make DESTDIR=${pkgdir} install
  # link the libstdc++ to the normal tree too so you always link to it by default
  mkdir -p "${pkgdir}/usr/lib"
  ln "${pkgdir}/usr/ngcc/lib/libstdc++.so.6.0.24" "${pkgdir}/usr/lib/libstdc++.so.6.0.24"
  # Do strip some things anyways...
  set +f 
  shopt -u extglob
  strip --strip-unneeded ${pkgdir}/usr/ngcc/libexec/gcc/*/*/{cc1,cc1plus,lto1}  ${pkgdir}/usr/ngcc/bin/*
}

