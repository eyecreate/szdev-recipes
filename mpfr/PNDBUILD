pkgname=libmpfr3
_pkgver=3.1.5
_patchlevel=p2
pkgver=${_pkgver}.${_patchlevel}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
url="http://www.mpfr.org/"
license=('LGPL')
depends=('libgmp3')
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz
        mpfr-${_pkgver}-${_patchlevel}.patch)
md5sums=('c4ac246cf9795a4491e7766002cd528f'
         'f388b7983bf69e2bfd3216426c89954c')

prepare() {
  cd "${srcdir}/mpfr-${_pkgver}"
  patch -p1 -i $srcdir/mpfr-${_pkgver}-${_patchlevel}.patch
}

build() {
  cd "${srcdir}/mpfr-${_pkgver}"
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
}

package() {
  cd "${srcdir}/mpfr-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
