pkgname=cmake
pkgver=2.8.12.2
pkgrel=2
pkgdesc="A cross-platform open-source make system"
url="http://www.cmake.org/"
license=('custom')
source=("http://www.cmake.org/files/v2.8/${pkgname}-${pkgver}.tar.gz")
md5sums=('17c6513483d23590cbce6957ec6d1e66')

build() {
  cd ${pkgname}-${pkgver}
#  export LDFLAGS="$LDFLAGS -ltinfo"
  ./bootstrap --prefix=/usr \
    --mandir=/share/man \
    --docdir=/share/doc/cmake \
    --parallel=6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  vimpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm644 Docs/cmake-indent.vim "${vimpath}"/indent/cmake-indent.vim
  install -Dm644 Docs/cmake-syntax.vim "${vimpath}"/syntax/cmake-syntax.vim

  install -Dm644 Copyright.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
